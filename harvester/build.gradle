buildscript {
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${version_springBoot}"
        classpath "se.transmode.gradle:gradle-docker:${version_gradleDocker}"
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:${version_bintray_plugin}"
    }

}

apply plugin: 'spring-boot'
apply plugin: 'docker'
apply plugin: 'com.jfrog.bintray'

dependencies {
    compile(
            project(':packrat'),
            'org.springframework.boot:spring-boot-starter-web',
            'org.springframework.boot:spring-boot-starter-actuator',
            "org.quartz-scheduler:quartz:${version_quartz}",
            "org.apache.httpcomponents:httpclient:${version_httpclient}",
    )

    testCompile(
            'org.springframework.boot:spring-boot-starter-test'
    )
}

bootRepackage {
    mainClass = 'com.github.reflectoring.infiniboard.harvester.HarvesterApplication'
    executable = true
}

task buildDocker(type: Docker, dependsOn: build) {
    push = false
    tagVersion = 'latest'
    tag = "${project.dockerGroup}/${jar.baseName}"

    dockerfile = file('src/main/docker/Dockerfile')
    doFirst {
        copy {
            from("${project.buildDir}/libs/") {
                rename "${jar.baseName}-.*\\.jar", "${jar.baseName}.jar"
            }
            into stageDir
            exclude '*.original'
        }
        copy {
            from "${projectDir}/src/main/docker"
            into stageDir
            include 'application.properties'
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')

    filesSpec {
        into version
        from('build/libs') {
            include "${project.name}-${project.version}.jar"
        }
    }

    pkg {
        userOrg = 'reflectoring'
        repo = 'infiniboard'
        name = 'releases'
        desc = 'official infiniboard release repository'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/reflectoring/infiniboard.git'
        websiteUrl = 'http://reflectoring.github.io/infiniboard/'

        publicDownloadNumbers = true

        version {
            name = project.version
            released = new Date()
        }
    }
}

bintrayUpload.dependsOn build
