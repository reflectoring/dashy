buildscript {
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${version_springBoot}"
        classpath "org.springframework:springloaded:${version_springLoaded}"
        classpath "se.transmode.gradle:gradle-docker:${version_gradleDocker}"
        classpath "com.sourcemuse.gradle.plugin:gradle-mongo-plugin:${version_mongo_plugin}"
    }
}

apply plugin: 'spring-boot'
apply plugin: 'mongo'
apply plugin: 'docker'
apply plugin: 'war'

configurations {
    providedRuntime
}

dependencies {
    compile(
            project(":packrat"),
            'org.springframework.boot:spring-boot-starter-web',
            'org.springframework.boot:spring-boot-starter-actuator',
            "org.tuckey:urlrewritefilter:${version_url_rewrite_filter}"
    )

    providedRuntime(
            "org.springframework.boot:spring-boot-starter-tomcat"
    )

    testCompile(
            'org.springframework.boot:spring-boot-starter-test',
    )
}

task integrationTest(type: Test) {
    runWithMongoDb = true // activates mongo plugin to start embedded MongoDB
    useJUnit {
        includeCategories project.MongoIntegrationTests
    }
}

bootRepackage {
    mainClass = 'com.github.reflectoring.Quartermaster'
    executable = true
}

bootRun {
    // default application configuration for running application via bootRun in development mode
    // (in production, these properties are defined in application.properties)
    jvmArgs '-Xdebug',
            '-Xrunjdwp:transport=dt_socket,server=y,suspend=n,address=9009'
}

task cleanWebappDir(type: Delete) {
    delete 'src/main/webapp'
}

task copyClient(type: Copy) {
    from '../infiniboard-app/build'
    into 'src/main/webapp'
}

// clean should also clean webapp dir
clean.dependsOn cleanWebappDir

// clean old webapp first
copyClient.dependsOn cleanWebappDir

// der client muss kopiert werden, bevor die quartermaster WAR gebaut wird
processResources.mustRunAfter copyClient

build.dependsOn copyClient

task buildDocker(type: Docker, dependsOn: build) {
    push = false
    tagVersion = 'latest'
    tag = "${project.dockerGroup}/${war.baseName}"

    dockerfile = file('src/main/docker/Dockerfile')
    doFirst {
        copy {
            from("${project.buildDir}/libs/") {
                rename "${war.baseName}-.*\\.war", "${war.baseName}.war"
            }
            into stageDir
            exclude '*.original'
        }
        copy {
            from "${projectDir}/src/main/docker"
            into stageDir
            include 'application.properties'
        }
    }
}
