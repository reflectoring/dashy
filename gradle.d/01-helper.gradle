def getEnv(String envVarName, Object defaultValue) {

    Map<String, String> env = System.getenv();
    if (env.containsKey(envVarName)) {
        return env.get(envVarName)
    }

    return defaultValue
}

boolean getBoolEnv(String envVarName, boolean defaultValue) {
    Boolean.valueOf(getEnv(envVarName, defaultValue))
}

task resolveDependencies {
    doLast {
        project.rootProject.allprojects.each { subProject ->
            subProject.buildscript.configurations.each { configuration ->
                resolveConfiguration(configuration)
            }
            subProject.configurations.each { configuration ->
                resolveConfiguration(configuration)
            }
        }
    }
}

void resolveConfiguration(configuration) {
    if (isResolveableConfiguration(configuration)) {
        configuration.resolve()
    }
}

boolean isResolveableConfiguration(configuration) {
    def nonResolveableConfigurations = ['apiElements', 'implementation',
                                        'runtimeElements', 'runtimeOnly',
                                        'testImplementation', 'testRuntimeOnly']

    if (nonResolveableConfigurations.contains(configuration.getName())) {
        return false
    }
    return true
}

ext {
    // export method as closure to make it available for all projects
    getEnv = this.&getEnv
    getBoolEnv = this.&getBoolEnv
}
