def getBuildVersion(String baseVersion) {
    def version = baseVersion;

    if (project.isCiBuild) {

        version += ".${project.buildNumber}"

        if (project.isPullRequest) {
            version += "-PR${project.pullRequestNumber}"
        }

    } else {
        version += "-local"
    }

    return version
}

ext {
// reads environment variables when running on snap-ci.com
    isCiBuild = getBoolEnv('CI', false)

    buildNumber = getEnv('CI_PIPELINE_IID', null)
    gitCommit = getEnv('CI_COMMIT_SHORT_SHA', null)
    pullRequestNumber = getEnv('CI_MERGE_REQUEST_IID', null)
    isPullRequest = pullRequestNumber != null

    // export method as closure to make it available for all projects
    getBuildVersion = this.&getBuildVersion
}

logger.lifecycle "isCiBuild        : ${isCiBuild}"
logger.lifecycle "buildNumber      : ${buildNumber}"
logger.lifecycle "gitCommit        : ${gitCommit}"
logger.lifecycle "isPullRequest    : ${isPullRequest}"
logger.lifecycle "pullRequestNumber: ${pullRequestNumber}"
logger.lifecycle ""
