project.ext {
    MongoIntegrationTests = 'com.github.reflectoring.infiniboard.test.categories.MongoIntegrationTests'
}

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.diffplug.spotless:spotless-plugin-gradle:${version_spotless}"
    }
}

subprojects {

    if (file('src/main/java').exists()) {
        apply plugin: 'jacoco'
        apply plugin: 'java'
        apply plugin: com.diffplug.gradle.spotless.SpotlessPlugin

        repositories {
            jcenter()
        }

        dependencies {
            compile(
                    "org.apache.commons:commons-lang3:${version_commons_lang}",
                    "commons-io:commons-io:${version_commons_io}",
            )
            testCompile(
                    project(':test-base'),
            )
        }

        spotless {
            java {
                googleJavaFormat()
            }
        }

        jacoco {
            toolVersion = version_jacoco
        }

        jacocoTestReport {
            reports {
                xml.enabled false
                csv.enabled false
                html.destination "${buildDir}/jacoco/html"
            }
        }

        build.dependsOn jacocoTestReport

        compileJava {
            sourceCompatibility = version_java
            targetCompatibility = version_java

            options.fork = true
        }

        test {
            useJUnit {
                // exclude all categories as default
                excludeCategories project.MongoIntegrationTests
            }

            // set heap size for the test JVM(s)
            minHeapSize = "128m"
            maxHeapSize = "1024m"

            jacoco {
                append = false
                includes = [
                        "com/github/**/*"
                ]
            }
        }

    } else {
        apply plugin: 'base'
    }

}
